EN BIT P3.2
RW BIT P3.1
RS BIT P3.0
DATABUS EQU P1
ORG 0H
MAIN:
	MOV R7,#0H
	MOV 50H,#1
	MOV 51H,#1
	MOV 52H,#1
	MOV 53H,#1
	MOV 54H,#1
	MOV P2,#0FH
	;MOV R4,#6FH; LUU GIA TRI MK NHAP
	MOV 5FH,#5 ; Luu do dai mat khau
	CLR P3.7
MP:
	MOV R4,#6FH
	CJNE R7,#0H,MP1
;HIEN THI MENU CHON LUA
	ACALL LCD_INIT
	MOV DPTR,#BEGIN1 
	ACALL DISP_FLINE ;HIEN THI DONG 1
	MOV DPTR,#BEGIN2
	ACALL DISP_SLINE ;HIEN THI DONG 2
MP_SB_0:
	ACALL SCAN_BTN
	CJNE R7,#1,MP_SB_01
	SJMP MP1
MP_SB_01:
	CJNE R7,#2,MP_SB_0
	SJMP MP2
MP1:
	CJNE R7,#1H,MP2
;MODE NHAP PASSWORD
	MOV DATABUS,#01H 
	ACALL SENDCOMMAND 
	MOV DPTR,#HT_NHAP_PASS
	ACALL DISP_FLINE
	MOV R2,#0BFH
	MOV 60H,#0H ;Luu gia tri do dai mk
MP_SB_1:
	ACALL SCAN_BTN
	CJNE R7,#0H,MP_SB_1
	SJMP MP
MP2:
	CJNE R7,#2H,MP
;MODE DOI PASSWORD
	MOV DATABUS,#01H
	ACALL SENDCOMMAND
	MOV DPTR,#PASS_CU
	ACALL DISP_FLINE
	MOV R2,#0BFH
	MOV 60H,#0H
MP_SB_2:
	ACALL SCAN_BTN
	CJNE R7,#0H,MP_SB_2
	SJMP MP

;*************************
SCAN_BTN:
	MOV R0,#0H	
SB:
	MOV DPTR,#MA_QUET
	MOV A,R0
	MOVC A,@A+DPTR
	MOV P2,A
	ACALL READ_CODE
	INC R0
	CJNE R0,#4,SB
	RET
;*************************
READ_CODE:
;READ CODE VER 2
	JB P2.0,RC0
	MOV R1,#0
	JNB P2.0,$
	SJMP RC3
RC0:
	JB P2.1,RC1
	MOV R1,#1
	JNB P2.1,$
	SJMP RC3
RC1:
	JB P2.2,RC2
	MOV R1,#2
	JNB P2.2,$
	SJMP RC3
RC2:
	JB P2.3,RC
	MOV R1,#3
	JNB P2.3,$
RC3:
	MOV A,#4
	MOV B,R1
	MUL AB
	ADD A,R0
	ACALL CHECK
RC:
	RET	
;*************************
CHECK:
CJNE R7,#0H,CKS1 ;CHECK CHO STATE_0
		CJNE A,#3,C1
		MOV R7,#1H
		RET
	C1:
		CJNE A,#7,C2
		MOV R7,#2H
		RET
	C2:
		RET
CKS1:
	ACALL CHECK_KEY
	RET
;*************************
CHECK_KEY:
	MOV B,#4 ;KIEM TRA A != 3,7,11,15
	PUSH ACC
	DIV AB
	POP ACC
	MOV R5,B
	CJNE R5,#3,CK_NEXT
	CJNE A,#3,CKB
	ACALL CHECK_PASS
	RET
CKB:
	CJNE A,#7,CKC
	DEC 60H ;giam gia tri bien dem
	MOV DATABUS,R2
	ACALL SENDCOMMAND
	MOV DATABUS,#" "
	ACALL SENDDATA
	DEC R2
	PUSH 0
	MOV 0,4
	MOV @R0,#0H
	POP 0
	DEC R4
	RET
CKC:
	CJNE A,#11,CKD
	ACALL SAVE_PASS
CKD:
	CJNE A,#15,CK_EXIT
	MOV R7,#0H
CK_EXIT:
	RET
CK_NEXT:
	INC 60H ;Tang gia tri bien dem
	ACALL PUTCHAR
	MOV DPTR,#GIA_TRI
	MOVC A,@A+DPTR
	PUSH ACC
CKE:
	POP 3
	INC R4
	PUSH ACC
	MOV A,R3
	PUSH 0
	MOV 0,4
	MOV @R0,A
	POP 0
	POP ACC
	RET
;*************************
CHECK_PASS:
	PUSH 0
	PUSH 1
	PUSH 5
	PUSH 3
	PUSH ACC
	MOV A,5FH
	XRL A,60H ;SO SANH DO DAI MK
	JZ CPT
CPF:
	MOV DATABUS,#01H 
	ACALL SENDCOMMAND 
	MOV DPTR,#SAI_MK
	ACALL DISP_FLINE ; THONG BAO SAI MK
	MOV R7,#0H
	MOV R5,#40 ; TAO TRE TG
CP:
	MOV TH0,#HIGH(-20000)
	MOV TL0,#LOW(-20000)
	ACALL DELAY
	DJNZ R5,CP
	SJMP CP_EXIT
CPT:
	MOV R5,60H
	MOV R0,#50H
	MOV R1,#70H
CP_AGAIN:
	MOV A,@R0
	MOV 3,@R1
	CJNE A,3,CPF ;NEU KHAC NHAU-> SAI MK
	INC R0
	INC R1
	DJNZ R5,CP_AGAIN
;MK DUNG
	CJNE R7,#1,CPT1
	CPL P3.7 ;BAT/TAT DEN
	MOV DATABUS,#01H 
	ACALL SENDCOMMAND 
	MOV DPTR,#NHAP_TC
	ACALL DISP_FLINE ; THONG BAO MK DUNG
	MOV R5,#30
CP0:
	MOV TH0,#HIGH(-20000)
	MOV TL0,#LOW(-20000)
	ACALL DELAY
	DJNZ R5,CP0
	MOV R7,#0H
	SJMP CP_EXIT
CPT1:
	CJNE R7,#2,CP_EXIT
	MOV DATABUS,#01H 
	ACALL SENDCOMMAND 
	MOV DPTR,#PASS_MOI
	ACALL DISP_FLINE ; THONG BAO NHAP MK MOI
	MOV R5,#30
CP1:
	MOV TH0,#HIGH(-20000)
	MOV TL0,#LOW(-20000)
	ACALL DELAY
	DJNZ R5,CP1
;NHAP LAI DIA CHI GHI C0H CHO R2
	MOV R2,#0BFH
	MOV 60H,#0H
	MOV R4,#6FH ; DIA CHI DE GHI MK MOI
CP_EXIT:
	POP ACC
	POP 3
	POP 5
	POP 1
	POP 0
	RET
;*************************
SAVE_PASS:
	PUSH 0
	PUSH 1
	PUSH 2
	PUSH ACC
;CAP NHAP DO DAI MK MOI
	MOV R2,60H
	MOV 5FH,R2
;CAP NHAP LAI MK MOI TAI VTRI RAM 50H
	MOV R1,#70H
	MOV R0,#50H
SV:
	MOV A,@R1
	MOV @R0,A
	INC R1
	INC R0
	DJNZ R2,SV
;HIEN THI THONG BAO LUU THANH CONG
	MOV DATABUS,#01H 
	ACALL SENDCOMMAND 
	MOV DPTR,#LUU_TC
	ACALL DISP_FLINE
	MOV R2,#30
SV0:
	MOV TH0,#HIGH(-20000)
	MOV TL0,#LOW(-20000)
	ACALL DELAY
	DJNZ R2,SV0
;**********************
	MOV R7,#0H
	POP ACC
	POP 2
	POP 1
	POP 0	
	RET
;*************************
PUTCHAR:
	INC R2
	MOV DATABUS,R2
	ACALL SENDCOMMAND
	MOV DATABUS,#"*"
	ACALL SENDDATA
	RET
;*************************
LCD_INIT:
CLR RS ;RS = 0 - GUI LENH
	CLR RW ;RW = 0 - WRITE LCD MODE
	SETB EN ;E = 1 - ENABLE
	MOV DATABUS,#38H ;CODE = 38H - 8 BIT, 16 CHAR/LINE, MATRIX 5x7
	ACALL SENDCOMMAND ;GUI LENH RA LCD
	MOV TH0,#HIGH(-4100)
	MOV TL0,#LOW(-4100)
	ACALL DELAY ;DELAY 4.1MS
	MOV DATABUS,#38H ;CODE = 38H - 8 BIT, 16 CHAR/LINE, MATRIX 5x7
	ACALL SENDCOMMAND ;GUI LENH RA LCD
	MOV TH0,#HIGH(-100)
	MOV TL0,#LOW(-100)
	ACALL DELAY ;DELAY 100US
	MOV DATABUS,#38H ;CODE = 38H - 8 BIT, 16 CHAR/LINE, MATRIX 5x7
	ACALL SENDCOMMAND ;GUI LENH RA LCD
	MOV DATABUS,#0CH ;CODE = C0H - CHO PHEP LCD HIEN THI
	ACALL SENDCOMMAND ;GUI LENH RA LCD
	MOV DATABUS,#01H ;CODE = 01H - XOA LCD
	ACALL SENDCOMMAND ;GUI LENH RA LCD
	RET
SENDCOMMAND: ;CTC GUI LENH (SENDCOMMAND) VA GUI DU LIEU (SENDDATA) RA LCD
	CLR RS ;RS = 0 - GUI LENH
	SJMP PULSE_EN
SENDDATA:
	SETB RS ;RS = 1 - GUI DU LIEU
	NOP
PULSE_EN: ;TAO XUNG ENABLE DE CHUYEN THONG TIN (COMMAND/DATA) VAO LCD
	CLR RW ;RW = 0 - WRITE LCD MODE
	setb EN ;EN = 0
	NOP
	CLR EN ;EN = 1 - XUNG ENABLE
	NOP
	;KIEM TRA CO BAO BAN (BUSY FLAG) DE DAM BAO HOAN TAT VIEC LCD GHI NHAN THONG TIN GUI DEN
	;*************************************************************
	MOV TH0,#HIGH(-1000) ;DOAN LENH NAY DUOC THAY THE CHO DOAN LENH KIEM TRA DUOI DAY
	MOV TL0,#LOW(-1000) ;KHI CHAY CHUONG TRINH NAY TRONG PHAN MEM MO PHONG TOPVIEW
	ACALL DELAY
	RET
	
DISP_FLINE: ;CTC DAT DIA CHI BAT DAU DONG 1 VA NAP DU LIEU DONG 1 VAO DDRAM
	MOV DATABUS,#80H ;CODE = 80H - DAT DDRAM DIA CHI BAT DAU CUA DONG 1 - 00H
	ACALL SENDCOMMAND ;GUI LENH RA LCD
	;NAP DIA CHI VUNG DU LIEU DONG 1 CUA LCD
	ACALL WRITE ;GUI VUNG DU LIEU SANG LCD
	RET
;***************************************************
DISP_SLINE: ;CTC DAT DIA CHI BAT DAU DONG 2 VA NAP DU LIEU DONG 2 VAO DDRAM
	MOV DATABUS,#0C0H ;CODE = C0H - DAT DDRAM DIA CHI BAT DAU CUA DONG 2 - 40H
	ACALL SENDCOMMAND ;GUI LENH RA LCD
	;NAP DIA CHI VUNG DU LIEU DONG 2 CUA LCD
	ACALL WRITE ;GUI VUNG DU LIEU SANG LCD
	RET
WRITE: ;CTC GUI DU LIEU SANG LCD, KET THUC GUI DU LIEU KHI DU LIEU GUI DI LA 99H
	MOV A,#0 ;OFFSET DAU TIEN TRONG VUNG DU LIEU CUA DPTR
	MOVC A,@A+DPTR ;LAY DU LIEU TU VUNG DU LIEU
	CJNE A,#99H,WRITE_CONT ;KIEM TRA LAY HET DU LIEU TRONG VUNG DU LIEU - 99H
	RET
WRITE_CONT: ;KHONG PHAI LA DU LIEU KET THUC - CHUA LAY HET DU LIEU
	MOV DATABUS,A ;CHUYEN DU LIEU CAN GUI RA PORT DEN LCD
	ACALL SENDDATA ;GUI DU LIEU RA LCD
	INC DPTR ;CHUYEN SANG DU LIEU KE TIEP
	SJMP WRITE ;QUAY TRO VE DE GUI DU LIEU KE TIEP
	RET
;***************************************************
DELAY:
	MOV TMOD,#01H
	SETB TR0
	JNB TF0,$
	CLR TR0
	CLR TF0
	RET
;**************************
ORG 300H
MA_QUET:
DB 0EFH,0DFH,0BFH,7FH
BEGIN1:
DB 'Nhap PASS: F1'
DB 99H
BEGIN2:
DB 'Doi PASS: F2'
DB 99H
HT_NHAP_PASS:
DB 'HAY NHAP PASS:'
DB 99H
PASS_CU:
DB 'NHAP PASS CU:'
DB 99H
PASS_MOI:
DB 'NHAP PASS MOI'
DB 99H
SAI_MK:
DB 'SAI MAT KHAU !'
DB 99H
NHAP_TC:
DB 'NHAP THANH CONG'
DB 99H
LUU_TC:
DB 'LUU THANH CONG'
DB 99H
GIA_TRI:
DB 7,8,9,0FFH,4,5,6,0FFH,1,2,3,0FFH,0AH,0,0BH,0FFH
END
